
#Область ОбработчикиСобытийФормы
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Параметры.Свойство("АдресЗначения", АдресЗначения); 
	
	Значение = ПолучитьИзВременногоХранилища(АдресЗначения);
	
	Если Значение = Неопределено Тогда
		Значение = Новый ТаблицаЗначений;
	КонецЕсли;
	
	Для Каждого Колонка ИЗ Значение.Колонки Цикл
		ДобавитьКолонку(Колонка.Имя, Колонка.ТипЗначения, "ТаблицаЗначений");
	КонецЦикла; 
	
	ТаблицаЗначений.Загрузить(Значение);
			
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы  

&НаКлиенте
Процедура КолонкиПриИзменении(Элемент)
	
	Элементы.ПрименитьИзмененияКолонок.Доступность = Истина;
	Элементы.КолонкиОтменитьРедактированиеКолонок.Доступность = Истина;
	ПроверитьТипКолонки(Элемент.ТекущиеДанные);
	
КонецПроцедуры  

&НаКлиенте
Процедура КолонкиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Элемент.ТекущийЭлемент = Элементы.КолонкиВыполнитьРекомендацию Тогда 
		
		ТекущиеДанные = Элемент.ТекущиеДанные;
		Если ТекущиеДанные.ТипНедоступенВЗапросе Тогда  
			ТекущиеДанные.ТипЗначения = ТекущиеДанные.ТипЗначенияРекомендованый;
			ТекущиеДанные.ТипНедоступенВЗапросе = Ложь;	  
			ТекущиеДанные.ТипЗначенияРекомендованый = Новый ОписаниеТипов;   
			ТекущиеДанные.ВыполнитьРекомендацию = "";   
			КолонкиПриИзменении(Элемент);
		КонецЕсли;
		
	КонецЕсли;
		
КонецПроцедуры


#КонецОбласти

#Область ОбработчикиКомандФормы
 &НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть(); 
	
КонецПроцедуры  

&НаКлиенте
Процедура Перенести(Команда)
	
	Если Элементы.ПрименитьИзмененияКолонок.Доступность Тогда  
        ОписаниеОповещения = Новый ОписаниеОповещения("ВопросПрименитьИзмененияКолонокЗавершение", ЭтаФорма);
		ПоказатьВопрос(ОписаниеОповещения, "Есть несохраненне изменения колонок таблицы. Применить их?"
			, РежимДиалогаВопрос.ДаНетОтмена);
        Возврат;
	КонецЕсли;
	
	ПеренестиЗакрыть(); 
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросПрименитьИзмененияКолонокЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Отмена Тогда
		Возврат    
	ИначеЕсли РезультатВопроса = КодВозвратаДиалога.Да Тогда
		ПрименитьИзмененияКолонокНаСервере();		
	КонецЕсли; 
	
	ПеренестиЗакрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ПеренестиЗакрыть()
	
	ПоместитьТаблицуВХранилище();	
	Закрыть(Истина);

КонецПроцедуры  

&НаКлиенте
Процедура ПрименитьИзмененияКолонок(Команда)  

	Если Не ПроверитьЗаполнение() Тогда
		Возврат
	КонецЕсли;
	
	ПрименитьИзмененияКолонокНаСервере();	
	
	Элементы.ПрименитьИзмененияКолонок.Доступность = Ложь;  
	Элементы.КолонкиОтменитьРедактированиеКолонок.Доступность = Ложь;

КонецПроцедуры

&НаКлиенте
Процедура ОтменитьРедактированиеКолонок(Команда)
	
	Колонки.Очистить();
	
	Для Каждого Колонка Из КолонкиКорректировка Цикл
		ЗаполнитьЗначенияСвойств(Колонки.Добавить(), Колонка);
	КонецЦикла;
		
	Элементы.ПрименитьИзмененияКолонок.Доступность = Ложь;  
	Элементы.КолонкиОтменитьРедактированиеКолонок.Доступность = Ложь;
	
КонецПроцедуры


#КонецОбласти

#Область СлужебныеПроцедурыИФункции 

&НаСервере
Функция ДобавитьКолонку(ИмяЭлемента, ТипЗначения, ИмяТаблицы, СоздатьЭлемент = Истина, ДобавитьВКолонки = Истина);
	
	Реквизит = Новый РеквизитФормы(ИмяЭлемента, ТипЗначения, ИмяТаблицы);
	МассивРеквизитов = Новый Массив;
	МассивРеквизитов.Добавить(Реквизит); 
	Попытка  // TODO отловить ошибку, на каком типе происходит исключение.
		ИзменитьРеквизиты(МассивРеквизитов); 
	Исключение       
		Реквизит.ТипЗначения = Новый ОписаниеТипов();
		ИзменитьРеквизиты(МассивРеквизитов); 
	КонецПопытки;
	
	Если СоздатьЭлемент Тогда
		НовыйЭлемент = Элементы.Добавить(ИмяЭлемента, Тип("ПолеФормы"), Элементы[ИмяТаблицы]);		
		НовыйЭлемент.ПутьКДанным = ИмяТаблицы + "." + ИмяЭлемента;
		НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
	КонецЕсли;
	
	Если ДобавитьВКолонки Тогда
		СтрокаКолонки = Колонки.Добавить();
		СтрокаКолонки.Имя = ИмяЭлемента;
		СтрокаКолонки.ТипЗначения = ТипЗначения;  
		ПроверитьТипКолонки(СтрокаКолонки);
		ЗаполнитьЗначенияСвойств(КолонкиКорректировка.Добавить(), СтрокаКолонки);
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ПроверитьТипКолонки(СтрокаКолонки)
	
	ОписаниеСтрока = Новый ОписаниеТипов("Строка",,,, Новый КвалификаторыСтроки(100));
	ОписаниеДата = Новый ОписаниеТипов("Дата",,,,, Новый КвалификаторыДаты(ЧастиДаты.ДатаВремя));
	
	Соответствие = Новый Соответствие();
	Соответствие.Вставить(Новый ОписаниеТипов, ОписаниеСтрока);
	Соответствие.Вставить(Новый ОписаниеТипов("Картинка"), ОписаниеСтрока);
	Соответствие.Вставить(Новый ОписаниеТипов("МоментВремени"), ОписаниеДата); 
	Соответствие.Вставить(Новый ОписаниеТипов("МоментВремени, NULL"), ОписаниеДата);  
	// TODO дополнить типы и искать по типам

	ПредложениеЗамены = Соответствие.Получить(СтрокаКолонки.ТипЗначения);
	Если ПредложениеЗамены <> Неопределено Тогда  
		СтрокаКолонки.ТипНедоступенВЗапросе = Истина;
		СтрокаКолонки.ТипЗначенияРекомендованый = ПредложениеЗамены; 
		СтрокаКолонки.ВыполнитьРекомендацию = "Заменить на: " + Строка(ПредложениеЗамены);
	Иначе
		СтрокаКолонки.ТипНедоступенВЗапросе = Ложь;
		СтрокаКолонки.ТипЗначенияРекомендованый = Новый ОписаниеТипов;  
		СтрокаКолонки.ВыполнитьРекомендацию = "";
	КонецЕсли;
		
КонецФункции

Процедура ПоместитьТаблицуВХранилище() 
	ПоместитьВоВременноеХранилище(ТаблицаЗначений.Выгрузить(), АдресЗначения);
КонецПроцедуры

&НаСервере
Процедура ПрименитьИзмененияКолонокНаСервере()  
	
	// Создаем новую версию таблицы. Отдельная таблица ТаблицаЗначенийКорректировка используется
	// чтобы вернутся к старой версии в случае ошибки создания новой версии таблицы. 
	Для Каждого Колонка Из Колонки Цикл
		ДобавитьКолонку(Колонка.Имя, Колонка.ТипЗначения, "ТаблицаЗначенийКорректировка", Ложь, Ложь);	
	КонецЦикла;
	
	// Переносим значения
	Для Каждого ТекущаяСтрока Из ТаблицаЗначений Цикл
		ЗаполнитьЗначенияСвойств(ТаблицаЗначенийКорректировка.Добавить(), ТекущаяСтрока);
	КонецЦикла; 
	
	// Очищаем копию таблицы колонок
	КолонкиКорректировка.Очистить();
	
	// Удалим старую таблицу из формы 
	МассивРеквизитов = Новый Массив;
	Для Каждого Реквизит Из ПолучитьРеквизиты("ТаблицаЗначений") Цикл
		МассивРеквизитов.Добавить("ТаблицаЗначений." + Реквизит.Имя);	
		Элементы.Удалить(Элементы.Найти(Реквизит.Имя));
	КонецЦикла;  
	ИзменитьРеквизиты(, МассивРеквизитов); 
	
	// Переносим новую версию в основную таблицу
	Для Каждого Колонка Из Колонки Цикл
		ДобавитьКолонку(Колонка.Имя, Колонка.ТипЗначения, "ТаблицаЗначений",, Ложь);
		ЗаполнитьЗначенияСвойств(КолонкиКорректировка.Добавить(), Колонка)
	КонецЦикла;   
	ТаблицаЗначений.Загрузить(ТаблицаЗначенийКорректировка.Выгрузить());
	
	// Удаляем таблицу корректировки  
	ТаблицаЗначенийКорректировка.Очистить();
	МассивРеквизитов = Новый Массив;
	Для Каждого Реквизит Из ПолучитьРеквизиты("ТаблицаЗначенийКорректировка") Цикл
		МассивРеквизитов.Добавить("ТаблицаЗначенийКорректировка." + Реквизит.Имя);	
	КонецЦикла;  
	ИзменитьРеквизиты(, МассивРеквизитов);
	
	Элементы.ПрименитьИзмененияКолонок.Доступность = Ложь; 
	
КонецПроцедуры

#КонецОбласти
