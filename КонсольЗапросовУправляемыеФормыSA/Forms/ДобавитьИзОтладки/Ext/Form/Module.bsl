
#Область ОбработчикиСобытийФормы
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("СтрокаОтладки", СтрокаОтладки); 
	
	ИмяОбработки = РеквизитФормыВЗначение("Объект").Метаданные().Имя;  
	
КонецПроцедуры


#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы 

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПодключитьОбработчикОжидания("ПроверитьНаличиеЗапросаВНастройках", 2, Ложь);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПроверитьНаличиеЗапросаВНастройках()
	
	Настройки = ПолучитьЗапросИзНастроекНаСервере();
	
	Если ЗначениеЗаполнено(Настройки) Тогда  
		Закрыть(Настройки);
	КонецЕсли;
		
КонецПроцедуры  

&НаСервере
Функция ПолучитьЗапросИзНастроекНаСервере()
	
	Отбор = Новый Структура("КлючОбъекта, КлючНастроек", ИмяОбработки, "ЗапросИзОтладки");
	Выборка = ХранилищеСистемныхНастроек.Выбрать(Отбор);
	
	Если Выборка.Следующий() Тогда
		
		Настройки = Выборка.Настройки;  
		
		// Помещаем таблицы значений во временное хранилище для передачи значения основной форме.
		Для Каждого КлючИЗначение Из Настройки.МенеджерВременныхТаблиц Цикл 	
			Адрес = ПоместитьВоВременноеХранилище(КлючИЗначение.Значение, Новый УникальныйИдентификатор());
			Настройки.МенеджерВременныхТаблиц[КлючИЗначение.Ключ] = Адрес;
		КонецЦикла;
		// Неотображаемые типы в параметрах
		РеквизитФормыВЗначение("Объект").ПоместитьНеотображаемыеПараметрыВХранилище(Настройки.Параметры, Новый УникальныйИдентификатор());
		
		// Удалим сохраненные данные из хранилища настроек.
		ХранилищеСистемныхНастроек.Удалить(Выборка.КлючОбъекта, Выборка.КлючНастроек, Выборка.Пользователь);
		
		Возврат Настройки;
		
	КонецЕсли;
		
КонецФункции

#КонецОбласти
